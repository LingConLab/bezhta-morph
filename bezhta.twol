Alphabet
	а ā б в г д ē ж з и ӣ й к л м ō о п р с т у ӯ ф х ц ч ш щ ъ ы ь ӏ э ю я %-
	ᴴ:ᴴ
	ᴴ:0
	%{и%}:0
	%{и%}:и
	
	%{V%}:0
	%{V%}:о
	%{V%}:и
	%{V%}:а
	
	%{V2%}:0
	%{V2%}:о
	
	%{C%}:0
	%{C%}:л
	
	%{gen%}:с
	%{gen%}:ш
	
	%{UM%}:0
	%{UM%}:ь
	
	%{з%}:з
	%{з%}:ж
	%{ц%}:ц
	%{ц%}:ч
	
	%{цӏ%}:цӏ
	%{цӏ%}:чӏ
	
	%{N%}:б
	%{N%}:м
	
	%{Cl%}:0
	
	е:е
	е:э
	
	н:н
	н:л
	
	о:0
	ь:0
	%{L%}:0
	%{L%}:и
;

Sets
!regular vowels	
  Vowels = а е и о у э ӣ ӯ ā ō ē;
!consonants
  Consonants = б в г д ж з й к л м н п р с т ф х ц ч ш щ;
!modifiers
  Mods = ь ъ ӏ;
! vowels that undergo nasalisation
  Nasalyzed = а е и о у ā ē ӣ ӯ;
! consonants that undergo assimilation  > ц when modified by ӏ
  Assimil = %{тӏ%} %{лӏ%} %{цӏ%};
Definitions

C = :Consonants [ :Mods ]*;
  
Rules

"vowel dropping in locative forms"
%{и%}:0 <=> _ X ; where X in (г х к);

"vowel dropping in locative forms"
%{и%}:0 <=> _ д о й;

"genI suffix realization с --> ш "
%{gen%}:ш <=> е _ ;

"genI suffix realization с --> ш "
%{gen%}:ш <=> Vowels ь _ ;

"lative suffix realization л --> ил"
%{V%}:и <=> л ӏ _ л;

"lative suffix realization л --> ил"
%{V%}:и <=> и (л) ъ _ л;

"lative suffix realization л --> ил"
%{V%}:и <=> о л ъ _ л;

"lative suffix realization л --> ил"
%{V%}:и <=> ō л ъ _ л;

"lative suffix realization л --> ил"
%{V%}:и <=> л а л ъ _ л;

"lative suffix realization л --> ол"
%{V%}:о <=> о ъ _ л;

"lative suffix realization л --> ол"
%{V%}:о <=> ō ъ _ л;

"lative suffix realization л --> ал"
%{V%}:а <=> а ъ _ л;

"ablative suffix realization с --> ис"
%{V%}:и <=> и ъ _ с; 

"ablative suffix realization с --> ос"
%{V%}:о <=> о ъ _ с; 

"ablative suffix realization с --> ас"
%{V%}:а <=> а ъ _ с;

"ablative suffix realization с --> ос"
%{V%}:о <=> ō ъ _ с; 

"ablative suffix realization с --> со"
%{V2%}:о <=> л X (%{V%}:) с _ ; where X in Mods;

"translative suffix realization"
%{C%}:л <=> ь а _ к ь ā;

"translative suffix realization"
%{C%}:л <=> ъ а _ к ь ā;

"vowel harmony"
%{UM%}:ь <=> Vowels ь X+ [ā | а | о ] _  ; where X in C;

"с --> ш"
%{с%}:ш <=> ь _ ; и _ е ; е: _ е ; а _ е ;

"цӏ --> чӏ"
%{цӏ%}:чӏ <=> и _ е ; е: _ е ; а _ е ;

"ц --> ч"
%{ц%}:ч <=> и _ е ; е: _ е ; а _ е ;

"з --> ж"
%{з%}:ж <=> и _ е ; е: _ е ; а _ е ;

"б --> м before nasalyzed"
%{N%}:м <=> .#. _ Nasalyzed (ь) ᴴ: ;

"nasalisation drop"
ᴴ:0 <=> .#. :м Vowels (ь) _;

"class marker I = 0 for е --> э rule to avoid wrong forms"
%{Cl%}:0 <=> _ ;

"е --> э"
е:э <=> %{Cl%}: _ ;

"н --> л before л" !Comrie et al. 2015 p. 100
н:л <=> _ л X; where X in Vowels;

"обо -> бо" 
о:0 <=> _ б о %{UM%}: .#.  ;

"vowel between two morphemes in verbs" 
%{L%}:и <=> X _ л ; where X in C;

"vowel between two morphemes in verbs" 
%{L%}:и <=> X _ й ; where X in C;

"vowel between two morphemes in verbs" 
о:0 <=> _ (:ь) :Vowels ;

"vowel between two morphemes in verbs" 
ь:0 <=> _ :Vowels ;
