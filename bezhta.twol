Alphabet
	а а̄ б в г д е е̄ ж з и ӣ й к л м н о о̄ п р с т у ӯ ф х ц ч ш щ ъ ы ь ӏ ᴴ э ю я

%{genI%}:с
%{genI%}:ш

%{и%}:0
%{и%}:и

%{л%}:л
%{л%}:ил
%{л%}:ол
%{л%}:ал

%{с%}:с
%{с%}:ис
%{с%}:ос
%{с%}:со
%{с%}:ас

%{кьа̄%}:кьа̄
%{кьа̄%}:лкьа̄

%{adj.pl%}:а
%{adj.pl%}:аь
%{adj.pl%}:ва̄

%{ла%}:ла
%{ла%}:лаь

%{а%}:а
%{а%}:0
%{а%}:аь

%{а̄%}:а̄
%{а̄%}:а̄ь
%{а̄%}:а̄ьᴴ

%{о%}:0
%{о%}:о
%{о%}:оь
%{о%}:оьᴴ

%{со%}:со
%{со%}:шоь

;

Sets
  Vowels = а е и о у ы э ю я ӣ ӯ а̄ о̄ е̄;
  Consonants = б в г д ж з й к л м н п р с т ф х ц ч ш щ;
  Mods = ь ъ ӏ;
 
Definitions

C = :Consonants [ :Mods ]*;
  
Rules

"genI suffix realization с --> ш "
%{genI%}:ш <=> е _ ;

"genI suffix realization с --> ш "
%{genI%}:ш <=> Vowels ь _ ;

"vowel dropping in locative forms"
%{и%}:0 <=> _ X ; where X in (г х к);

"vowel dropping in locative forms"
%{и%}:0 <=> _ д о й;

"lative suffix realization л --> ил"
%{л%}:ил <=> л ӏ _ ;

"lative suffix realization л --> ил"
%{л%}:ил <=> и (л) ъ _ ;

"lative suffix realization л --> ил"
%{л%}:ил <=> о л ъ _ ;

"lative suffix realization л --> ил"
%{л%}:ил <=> о̄ л ъ _ ;

"lative suffix realization л --> ил"
%{л%}:ил <=> л а л ъ  _ ;

"lative suffix realization л --> ол"
%{л%}:ол <=> о ъ _ ;

"lative suffix realization л --> ол"
%{л%}:ол <=> о̄ ъ _ ;

"lative suffix realization л --> ал"
%{л%}:ал <=> а ъ _ ;

"ablative suffix realization с --> ис"
%{с%}:ис <=> и ъ _ ; 

"ablative suffix realization с --> ос"
%{с%}:ос <=> о ъ _ ; 

"ablative suffix realization с --> ас"
%{с%}:ас <=> а ъ _ ;

"ablative suffix realization с --> ос"
%{с%}:ос <=> о̄ ъ _ ; 

"ablative suffix realization с --> со"
%{с%}:со <=> л X _ ; where X in Mods;

"translative suffix realization"
%{кьа̄%}:лкьа̄ <=> ь а _ ;

"translative suffix realization"
%{кьа̄%}:лкьа̄ <=> ъ а _ ;

"avar adjective plural suffix realization"
%{ла%}:ла <=> Vowels б _ ; 

"avar adjective plural suffix realization"
%{ла%}:лаь <=> Vowels ь б _ ; 

"adjective plural suffix realization"
%{adj.pl%}:ва̄ <=> о _ ; 

"adjective plural suffix realization"
%{adj.pl%}:аь <=> ш _ ;

"a -> аь vowel harmony"
%{а%}:аь <=> Vowels ь X+ _  ; where X in C;

"adjective suffix realization a -> аь"
%{а%}:0 <=> а̄ _  ;

"ā -> а̄ь vowel harmony"
%{а̄%}:а̄ь <=> Vowels ь X+ _  ; where X in C; 

"o -> oь vowel harmony"
%{о%}:оь <=> Vowels ь X+ _  ; where X in C; 

"adjective singular suffix realization"
%{о%}:0 <=> а̄ _  ;

"ā -> а̄ьᴴ vowel harmony"
%{а̄%}:а̄ьᴴ <=> Vowels ь ᴴ X+ _  ; where X in C;

"o -> oьᴴ vowel harmony"
%{о%}:оьᴴ <=> Vowels ь ᴴ X+ _  ; where X in C;


"adj emphatic suffix realization со --> шоь"
%{со%}:шоь <=> %{о%}:оь _ ;