Alphabet
	а а̄ б в г д е е̄ ж з и ӣ й к л м н о о̄ п р с т у ӯ ф х ц ч ш щ ъ ы ь ӏ ᴴ э э̄ ю я 
	%{и%}:0
	%{и%}:и
	
	%{V%}:0
	%{V%}:о
	%{V%}:и
	%{V%}:а
	
	%{V2%}:0
	%{V2%}:о
	
	%{C%}:0
	%{C%}:л
	
	%{gen%}:с
	%{gen%}:ш
	
	%{UM%}:0
	%{UM%}:ь
	
	%{з%}:з
	%{з%}:ж
	%{ц%}:ц
	%{ц%}:ч
	%{цӏ%}:цӏ
	%{цӏ%}:чӏ
	
	%{N%}:б
	%{N%}:м
	
;

Sets
  Vowels = а е и о у ы э ю я ӣ ӯ а̄ о̄ е̄ э̄;
  Consonants = б в г д ж з й к л м н п р с т ф х ц ч ш щ;
  Mods = ь ъ ӏ;
 
Definitions

C = :Consonants [ :Mods ]*;
  
Rules

"vowel dropping in locative forms"
%{и%}:0 <=> _ X ; where X in (г х к);

"vowel dropping in locative forms"
%{и%}:0 <=> _ д о й;

"genI suffix realization с --> ш "
%{gen%}:ш <=> е _ ;

"genI suffix realization с --> ш "
%{gen%}:ш <=> Vowels ь _ ;

"lative suffix realization л --> ил"
%{V%}:и <=> л ӏ _ л;

"lative suffix realization л --> ил"
%{V%}:и <=> и (л) ъ _ л;

"lative suffix realization л --> ил"
%{V%}:и <=> о л ъ _ л;

"lative suffix realization л --> ил"
%{V%}:и <=> о̄ л ъ _ л;

"lative suffix realization л --> ил"
%{V%}:и <=> л а л ъ _ л;

"lative suffix realization л --> ол"
%{V%}:о <=> о ъ _ л;

"lative suffix realization л --> ол"
%{V%}:о <=> о̄ ъ _ л;

"lative suffix realization л --> ал"
%{V%}:а <=> а ъ _ л;

"ablative suffix realization с --> ис"
%{V%}:и <=> и ъ _ с; 

"ablative suffix realization с --> ос"
%{V%}:о <=> о ъ _ с; 

"ablative suffix realization с --> ас"
%{V%}:а <=> а ъ _ с;

"ablative suffix realization с --> ос"
%{V%}:о <=> о̄ ъ _ с; 

"ablative suffix realization с --> со"
%{V2%}:о <=> л X (%{V%}:) с _ ; where X in Mods;

"translative suffix realization"
%{C%}:л <=> ь а _ к ь а̄;

"translative suffix realization"
%{C%}:л <=> ъ а _ к ь а̄;

"vowel harmony"
%{UM%}:ь <=> Vowels ь X+ [а̄ | а | о ] _  ; where X in C;

"с --> ш"
%{с%}:ш <=> ь _ ; и _ е ; е _ е ; а _ е ;

"цӏ --> чӏ"
%{цӏ%}:чӏ <=> и _ е ; е _ е ; а _ е ;

"ц --> ч"
%{ц%}:ч <=> и _ е ; е _ е ; а _ е ;

"з --> ж"
%{з%}:ж <=> и _ е ; е _ е ; а _ е ;

"б --> м before nasals"
%{N%}:м <=> # _ Vowels (ь) ᴴ; 

