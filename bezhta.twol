Alphabet
	а а̄ б в г д е е̄ ж з и ӣ й к л м н о о̄ п р с т у ӯ ф х ц ч ш щ ъ ы ь ӏ ᴴ э э̄ ю я %-
	д:б н:л

%{genI%}:с
%{genI%}:ш

%{и%}:0
%{и%}:и

%{л%}:л
%{л%}:ил
%{л%}:ол
%{л%}:ал

%{с%}:с
%{с%}:ис
%{с%}:ос
%{с%}:со
%{с%}:ас

%{кьа̄%}:кьа̄
%{кьа̄%}:лкьа̄

%{ла%}:ла
%{ла%}:лаь

%{а%}:а
%{а%}:аь

%{а̄%}:а̄
%{а̄%}:а̄ь
%{а̄%}:а̄ьᴴ

%{о%}:о
%{о%}:оь
%{о%}:оьᴴ

%{со%}:со
%{со%}:шоь
;

Sets
  Vowels = а е и о у ы э ю я ӣ ӯ а̄ о̄ е̄ э̄;
  Consonants = б в г д ж з й к л м н п р с т ф х ц ч ш щ;
  Mods = ь ъ ӏ;
 
Definitions

C = :Consonants [ :Mods ]*;
  
Rules

"genI suffix realization с --> ш "
%{genI%}:ш <=> е _ ;

"genI suffix realization с --> ш "
%{genI%}:ш <=> Vowels ь _ ;

"vowel dropping in locative forms"
%{и%}:0 <=> _ X ; where X in (г х к);

"vowel dropping in locative forms"
%{и%}:0 <=> _ д о й;

"lative suffix realization л --> ил"
%{л%}:ил <=> л ӏ _ ;

"lative suffix realization л --> ил"
%{л%}:ил <=> и (л) ъ _ ;

"lative suffix realization л --> ил"
%{л%}:ил <=> о л ъ _ ;

"lative suffix realization л --> ил"
%{л%}:ил <=> о̄ л ъ _ ;

"lative suffix realization л --> ил"
%{л%}:ил <=> л %{а%}:а л ъ  _ ;

"lative suffix realization л --> ол"
%{л%}:ол <=> о ъ _ ;

"lative suffix realization л --> ол"
%{л%}:ол <=> о̄ ъ _ ;

"lative suffix realization л --> ал"
%{л%}:ал <=> %{а%}:а ъ _ ;

"ablative suffix realization с --> ис"
%{с%}:ис <=> и ъ _ ; 

"ablative suffix realization с --> ос"
%{с%}:ос <=> о ъ _ ; 

"ablative suffix realization с --> ас"
%{с%}:ас <=> а ъ _ ;

"ablative suffix realization с --> ос"
%{с%}:ос <=> о̄ ъ _ ; 

"ablative suffix realization с --> со"
%{с%}:со <=> л X _ ; where X in Mods;

"translative suffix realization"
%{кьа̄%}:лкьа̄ <=> ь а _ ;

"translative suffix realization"
%{кьа̄%}:лкьа̄ <=> ъ а _ ;

"a -> аь vowel harmony"
%{а%}:аь <=> Vowels ь X+ _  ; where X in C;

"ā -> а̄ь vowel harmony"
%{а̄%}:а̄ь <=> Vowels ь X+ _  ; where X in C; 

"o -> oь vowel harmony"
%{о%}:оь <=> Vowels ь X+ _  ; where X in C; 

"ā -> а̄ьᴴ vowel harmony"
%{а̄%}:а̄ьᴴ <=> Vowels ь ᴴ X+ _  ; where X in C;

"o -> oьᴴ vowel harmony"
%{о%}:оьᴴ <=> Vowels ь ᴴ X+ _  ; where X in C;

"regressive assimilation д --> б before б"
д:б <=>  _ б ;

"regressive assimilation н --> л before л"
н:л <=>  _ л ;

"б --> м assimilation before nasals"
б:м <=> # _ Vowels (ь) ᴴ ;

"denasalization"
ᴴ:0 <=> # б Vowels (ь) _ ;
