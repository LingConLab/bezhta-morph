PATTERNS
Verb > Question
ClassVerb > Question

PATTERN Imperative
Mood[imp] > Mood[rog]
Mood[imp] > ([:а]? Mood[prmI]) > [<prmII>:ъ]
Mood[prmIII]

PATTERN Valency
([:и] | [:е] | [:о] | [:а])? Antipassive
([:и] | [:е] | [:о] | [:а])? Causative

PATTERN TAM
#--- positive forms ---
Infinitive > Mood[dub] # dubitative mood
Past | Present | Future > Mood[hyp] #	hypothetical mood
Plusquamperfect
Imperative
Mood[proh]
Mood[opt]
([:и] | [:е] | [:о])? Mood[cond] > ([<invc>:л] | [<cnc>:на])	#	conditional, concessive conditional
Past | Present Mood[irr]	#	probabilitive (irreal conditional)
([:и] | [:е] | [:о])? Participle[prs]
Participle[pst]
Infinitive Participle[fut]

#--- negative forms ---
Negation[-neg]
Mood[neg.opt]
Negation[neg] Mood[cond] > ([<invc>:л] | [<cnc>:на])
Negation[neg] Participle

PATTERN Verb
VerbRoot(1)[-cl] :VerbNumberInfix(1) VerbRoot(2)[-cl] VerbNumberInfix(2): TAM

PATTERN ClassVerb

VerbRoot(1)[cl] :ClassInfix(1) VerbRoot(2)[cl] ClassInfix(2): TAM
Class VerbRoot(1)[cl] :VerbNumberInfix(1)[sg] VerbRoot(2)[cl] VerbNumberInfix(2)[sg]: TAM
ClassPl VerbRoot(1)[cl] :VerbNumberInfix(1)[-sg] VerbRoot(2)[cl] VerbNumberInfix(2)[-sg]: TAM

LEXICON VerbRoot(2)
а[cl] гъ[cl] # снимать
зо гъ # находиться
оь л # хватать
го т # постелить
г[cl] в[cl] # идти
г[cl] л[cl] # класть
т[cl] кӏ[cl] # использовать
тӏ[cl] тӏ[cl] # бросить
г[cl] цӏ[cl] # появиться
у[cl] гъ[cl] # умирать
о[cl] лъ[cl] # заканчиваться
ги тӏ # налить
их[cl] лъ[cl] #согреться

LEXICON VerbNumberInfix(2)
:[sg] <sg>:[sg]
:а <pl>:а
:ба <pl>:ба
:ва <pl>:ва
:ваба <pl>:ваба
:йа <pl>:йа
:йаба <pl>:йаба

LEXICON ClassInfix(2)
:о <I>:о
:и <II>:и
:у <III/IV>:у

LEXICON Causative 
<caus>:л
<caus>:лл

LEXICON Antipassive
<antip>:л{а}
<antip>:д{а}
<antip>:в{а}
<antip>:{а}

LEXICON Transitivity
<tr>:кӏ
<intr>:к
<tr>:лӏ
<tr>:л
<intr>:лъ

#---- Tense ----
LEXICON Past # законченное прошедшее (аорист), (Forker 2011) глоссирует как WPST
<pst>:ийо
<pst>:йо
<pst>:ойо
<pst>:л{о}

LEXICON Negation
<neg>:{а}ъ[neg]
<neg><pst>:еъеш[pst]
<neg><pst>:йъеш[pst]
<neg><prs>:аъас #Гласные основы глагола е, и, о переходят в а, гласный корня -а- гласный исчезает перед гласным аффикса.
<neg><prs>:аьъаьш 
<neg><imp>:ассаъ
<neg><imp>:ссаъ
<neg><imp>:аьшшаьъ
<neg><imp>:шшаьъ
<neg><cnvb>:ечӏе
<neg><cnvb>:йчӏе

LEXICON Present
<prs>:ца
<prs>:чаь
<prs>:с
<prs>:ш
<prs>:ис
<prs>:ос
<prs>:оьщ

LEXICON Future
<fut>:ас
<fut>:аьш

LEXICON Plusquamperfect
<ppf>:а̄гьийо
<ppf>:а̄ьгьийо

#---- Mood ----

LEXICON Mood
<imp>:{а}[imp]
<imp>:[imp]
<imp>:и[imp]
<imp>:е[imp]
<imp>:{о}[imp]
<proh>:а̄къа[proh]
<opt>:акъос[opt]
<opt>:къос[opt]
<neg.opt>:акъаъас[opt.neg]
<neg.opt>:къаъас[opt.neg]
<rog>:ло̄[rog]
<rog>:ловале[rog]
<rog>:лоало[rog]
<prmI>:л{а}[prmI]
<prmII>:ъ[prmII]
<prmIII>:{а}ла̄[prmIII]
<cond>:да[cond]
<irr>:къода̄[irr]
<dub>:ла̄[dub]
<dub>:ла̄ъ[dub]
<hyp>:ле[hyp]

#---- Aspect -----
LEXICON Aspect
<dur>:ла
<dur>:ва
<dur>:да
<dur>:а

#---- Non-finite forms ----

LEXICON Infinitive 
<inf>:{а}л

LEXICON Masdar
<nmlz>:лъи
<nmlz>:ни

LEXICON Participle
<ptsp.prs>:цас[prs]
<ptsp.prs>:чаьш[prs]
<ptsp.pst>:йо[pst]
<ptsp.pst>:ийо[pst]
<ptsp.fut>:лъийо[fut]

# ---- Discourse ----
LEXICON Question
<q>:д
<q>:ди
<q>:лӏо̄

LEXICON Focus
<foc>:зу

